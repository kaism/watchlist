apply plugin: 'com.android.application'

// load local config file
def configPropertiesFile = rootProject.file("config.properties")
def configProperties = new Properties()
configProperties.load(new FileInputStream(configPropertiesFile))

android {
	compileSdkVersion 29
	buildToolsVersion '29.0.3'

	defaultConfig {
		applicationId 'com.github.kaism.watchlist'
		minSdkVersion 22
		targetSdkVersion 29
		versionCode 1
		versionName '1.0'

		testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
		testInstrumentationRunnerArguments clearPackageData: 'true'		// clear data after each test

		// get api url and key from local config file
		buildConfigField('String', 'QUOTEAPI_URL', configProperties['QUOTEAPI_URL'])
		buildConfigField('String', 'QUOTEAPI_KEY', configProperties['QUOTEAPI_KEY'])
		buildConfigField('String', 'QUICKSEED_CSV', configProperties['QUICKSEED_CSV'])
	}
	buildTypes {
		release {
			minifyEnabled false
			proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
		}
	}

	testOptions {
		execution 'ANDROIDX_TEST_ORCHESTRATOR'
	}

}

dependencies {
	// design
	implementation 'com.google.android.material:material:1.2.0'

	// user interface
	implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'

	// database
	implementation 'androidx.room:room-runtime:2.2.5'
	annotationProcessor 'androidx.room:room-compiler:2.2.5'

	// lifecycle
	implementation 'androidx.lifecycle:lifecycle-viewmodel:2.2.0'
	implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'

	// Retrofit
	implementation 'com.squareup.retrofit2:retrofit:2.5.0'
	implementation 'com.squareup.retrofit2:converter-gson:2.5.0'

	// testing
	androidTestImplementation 'androidx.test.espresso:espresso-intents:3.3.0'
	androidTestImplementation 'androidx.test:runner:1.3.0'
	androidTestImplementation 'androidx.test.ext:junit:1.1.2'
	androidTestUtil 'androidx.test:orchestrator:1.3.0'
}